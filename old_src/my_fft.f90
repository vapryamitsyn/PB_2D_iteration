    MODULE myfft
    USE BOX
    USE OMP_LIB
    USE mkl_service
    USE MKL_DFTI
    IMPLICIT NONE
    TYPE(DFTI_DESCRIPTOR),PRIVATE, POINTER :: forHAND => NULL(),backHAND => NULL()
    INTEGER,PUBLIC:: NUT
    REAL(8),PRIVATE :: XTEST
    PRIVATE :: FFT_CHECK_ERROR
    PUBLIC :: INIT_FFT,END_FFT,FORWARD_FFT,BACKWARD_FFT,CONVOLUTE
    COMPLEX(8),PUBLIC :: QFR(NT2),QFRT(NT2)

    CONTAINS 

    SUBROUTINE INIT_FFT()
    INTEGER :: STATUS = 0
    !$OMP PARALLEL DEFAULT (SHARED)
    nut=OMP_GET_NUM_THREADS()
    print*,"NUT=",OMP_GET_THREAD_NUM()," of ",nut
    !$OMP END PARALLEL  
    CALL MKL_SET_NUM_THREADS(NUT)
    !CALL MKL_SET_NUM_THREADS(1)
    !call KMP_SET_LIBRARY_SERIAL()
    call  KMP_SET_LIBRARY_TURNAROUND()
    PRINT*, "MKL threads:",MKL_GET_MAX_THREADS()
    print *,"Create DFTI descriptor for real transform"
    !STATUS = DFTICREATEDESCRIPTOR(HAND, DFTI_SINGLE, DFTI_REAL, 3, [NX,NY,NZ])
    STATUS = DFTICREATEDESCRIPTOR(forHAND, DFTI_DOUBLE, DFTI_REAL, 3, [NX,NY,NZ])
    CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DFTICREATEDESCRIPTOR(backHAND, DFTI_DOUBLE, DFTI_REAL, 3, [NX,NY,NZ])
    CALL FFT_CHECK_ERROR(STATUS)

    print *,"Set out-of-place"
    STATUS = DFTISETVALUE(forHAND, DFTI_PLACEMENT, DFTI_NOT_INPLACE)
    CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DFTISETVALUE(backHAND, DFTI_PLACEMENT, DFTI_NOT_INPLACE)
    CALL FFT_CHECK_ERROR(STATUS)


    print *,"Set CCE storage"
    STATUS = DFTISETVALUE(FORHAND, DFTI_CONJUGATE_EVEN_STORAGE, DFTI_COMPLEX_COMPLEX)
    CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DFTISETVALUE(BACKHAND, DFTI_CONJUGATE_EVEN_STORAGE, DFTI_COMPLEX_COMPLEX)
    CALL FFT_CHECK_ERROR(STATUS)

    CSTRIDES = [0, 1, NX/2+1, NY*(NX/2+1)]
    RSTRIDES = [0, 1, NX,     NY*NX]



    STATUS = DftiSetValue(forHAND, DFTI_BACKWARD_SCALE,1._8/real(NT,8))
    CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DftiSetValue(backHAND, DFTI_BACKWARD_SCALE,1._8/real(NT,8))
    CALL FFT_CHECK_ERROR(STATUS)


    print *,"Commit DFTI descriptor"
    STATUS = DFTISETVALUE(forHAND, DFTI_INPUT_STRIDES, RSTRIDES)  ;CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DFTISETVALUE(forHAND, DFTI_OUTPUT_STRIDES, CSTRIDES) ;CALL FFT_CHECK_ERROR(STATUS)


    STATUS =DFTISETVALUE(BACKHAND, DFTI_INPUT_STRIDES, CSTRIDES)    ;CALL FFT_CHECK_ERROR(STATUS)
    STATUS =DFTISETVALUE(BACKHAND, DFTI_OUTPUT_STRIDES, RSTRIDES)   ;CALL FFT_CHECK_ERROR(STATUS)

    STATUS =DFTICOMMITDESCRIPTOR(BACKHAND)                          ;CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DFTICOMMITDESCRIPTOR(FORHAND)                        ;CALL FFT_CHECK_ERROR(STATUS)



    !yet anothder stupid test
    !STATUS = DftiGetValue(forHAND, DFTI_INPUT_STRIDES, xRSTRIDES)  ;CALL FFT_CHECK_ERROR(STATUS)
    !STATUS = DftiGetValue(forHAND, DFTI_OUTPUT_STRIDES, xCSTRIDES) ;CALL FFT_CHECK_ERROR(STATUS)
    !STATUS =DftiGetValue(BACKHAND, DFTI_INPUT_STRIDES, xCSTRIDES)    ;CALL FFT_CHECK_ERROR(STATUS)
    !STATUS =DftiGetValue(BACKHAND, DFTI_OUTPUT_STRIDES, xRSTRIDES)   ;CALL FFT_CHECK_ERROR(STATUS)
    !end of anothder stupid test
    RETURN
    !contains
    END SUBROUTINE INIT_FFT

    SUBROUTINE FFT_CHECK_ERROR(STATUS)
    INTEGER,INTENT(IN)::STATUS
    IF (STATUS .NE. 0) THEN
        IF (.NOT. DFTIERRORCLASS(STATUS,DFTI_NO_ERROR)) THEN
            PRINT *, "Error:", DftiErrorMessage(status)
            STOP "FFT error"
        ENDIF
    ENDIF
    END SUBROUTINE FFT_CHECK_ERROR



    SUBROUTINE END_FFT()
    INTEGER :: STATUS = 0 
    PRINT *,"RELEASE THE DFTI DESCRIPTOR"
    STATUS = DFTIFREEDESCRIPTOR(forHAND);CALL FFT_CHECK_ERROR(STATUS)
    STATUS = DFTIFREEDESCRIPTOR(backHAND);CALL FFT_CHECK_ERROR(STATUS)
    END SUBROUTINE END_FFT


    SUBROUTINE FORWARD_FFT(QQ,QFR)
    REAL(8), intent(in) :: QQ(NT)
    COMPLEX(8),intent(out) :: QFR(NT2)
    integer :: status
    status = DftiComputeForward(forhand, QQ, QFR); CALL FFT_CHECK_ERROR(STATUS)
    END SUBROUTINE FORWARD_FFT

    SUBROUTINE BACKWARD_FFT(QFR,QQ)
    REAL(8), intent(out) :: QQ(NT)
    COMPLEX(8),intent(in) :: QFR(NT2)
    integer :: status
    status = DFTICOMPUTEBACKWARD(backHAND, QFR, QQ) ; CALL FFT_CHECK_ERROR(STATUS)
    END SUBROUTINE BACKWARD_FFT

    SUBROUTINE CONVOLUTE(RQ,IQ,RQI)
    REAL,INTENT(IN):: IQ(NT2)
    REAL(8),INTENT(IN):: RQ(NT)
    REAL(8),INTENT(OUT):: RQI(NT)
    INTEGER:: I
    CALL FORWARD_FFT(RQ,QFR)
    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFR(I)=IQ(I)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 
    CALL BACKWARD_FFT(QFR,RQI) 
    END SUBROUTINE CONVOLUTE


    SUBROUTINE gCONVOLUTE(RQ,IQ,RQI)
    REAL,INTENT(IN):: IQ(NT2,3)
    REAL(8),INTENT(IN):: RQ(NT)
    REAL(8),INTENT(OUT):: RQI(NT,3)
    INTEGER:: I
    CALL FORWARD_FFT(RQ,QFR)
    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFRT(I)=(0.,1.)*IQ(I,1)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 
    CALL BACKWARD_FFT(QFRT,RQI(:,1))

    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFRT(I)=(0.,1.)*IQ(I,2)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 
    CALL BACKWARD_FFT(QFRT,RQI(:,2))

    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFRT(I)=(0.,1.)*IQ(I,3)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 
    CALL BACKWARD_FFT(QFRT,RQI(:,3))

    END SUBROUTINE gCONVOLUTE



    SUBROUTINE dCONVOLUTE(RQ,IQ,RQI)
    REAL,INTENT(IN):: IQ(NT2,3)
    REAL(8),INTENT(IN):: RQ(NT,3)
    REAL(8),INTENT(OUT):: RQI(NT)
    INTEGER:: I
    CALL FORWARD_FFT(RQ(:,1),QFR)
    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFRT(I)=(0.,1.)*IQ(I,1)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 
    CALL FORWARD_FFT(RQ(:,2),QFR)

    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFRT(I)=QFRT(I)+(0.,1.)*IQ(I,2)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 

    CALL FORWARD_FFT(RQ(:,3),QFR)
    !$OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(I)
    DO I=1,NT2
        QFRT(I)=QFRT(I)+(0.,1.)*IQ(I,3)*QFR(I)
    ENDDO
    !$OMP END PARALLEL DO 

    CALL BACKWARD_FFT(QFRT,RQI) 
    END SUBROUTINE dCONVOLUTE

    subroutine dump_fft(qz,fn)
    character(*),intent(in) ::fn 
    complex(8),intent(in) :: qz(nt2)
    integer :: I
    OPEN(UNIT=299,FILE=fn,FORM ='FORMATTED',ACTION='WRITE',STATUS='REPLACE')
    WRITE(299,'(" ",G36.22 , " , " ,G36.22)')  (qz(i),i=1,nt2)
    CLOSE(299)
    end subroutine dump_fft

    END MODULE myfft
